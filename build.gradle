import groovy.json.JsonParserType
import java.util.regex.Pattern;


buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
    buildDir = rootProject.projectDir.toString() + "/build/${project.name}"
}

subprojects{ subproject ->
    switch (subproject.path) {
        case ':Lineage_Dialer':
            apply plugin:  'com.android.application'
            apply plugin: 'com.google.protobuf'

            // android.applicationVariants.all { variant ->
            //     variant.mergeResources.doFirst {
            //         println it
            //         exit 0
            //     }
            // }

            android {
                aaptOptions {
                  def packages = file(subproject.projectDir.path + '/packages.mk').text
                  .findAll(Pattern.compile(/(([A-Za-z]{1}[A-Za-z\d_]*\.)+[A-Za-z][A-Za-z\d_]*)/,Pattern.MULTILINE)) - 'AndroidManifest.xml'
                  
                  additionalParameters "--extra-packages", packages.join(':')
                  additionalParameters "--auto-add-overlay"

                }
                sourceSets {
                    main{
                        java {
                            srcDirs = [subproject.projectDir.toString()]
                            gradle.blacklist.each{
                                    exclude ('**/' + it + '/*.java')
                                }
                            srcDirs += (rootProject.projectDir.toString() + '/patch')
                        }
                        proto {
                            srcDirs = [subproject.projectDir.toString()]
                        }
                    }
                }
            }
            configurations {
              compile.exclude group: 'com.google.protobuf', module: 'protobuf-java'
            }
            protobuf {
                protoc {
                    artifact = 'com.google.protobuf:protoc:3.9.2'
                }
                plugins {
                    javalite {
                        artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
                    }
                }
                generateProtoTasks {
                    all().each { task ->
                        task.builtins {
                            remove java
                        }
                        task.plugins {
                            javalite {}
                        }
                    }
                }
            }      
            dependencies {
              provided files(rootProject.projectDir.toString() + '/lib/android.jar')
              //implementation project(":com.android.common")
              implementation project(":contacts.common")
              implementation project(":dialer.about")
              implementation project(":dialer.app")
              implementation project(":dialer.app.manifests.activities")
              //implementation project(":dialer.app.voicemail.error")
              implementation project(":assets.product")
              implementation project(":assets.quantum")
              implementation project(":dialer.blocking")
              implementation project(":dialer.callcomposer")
              implementation project(":dialer.callcomposer.camera.camerafocus")
              implementation project(":dialer.callcomposer.cameraui")
              implementation project(":dialer.calldetails")
              implementation project(":dialer.calllog.ui")
              implementation project(":dialer.calllogutils")
              implementation project(":dialer.common")
              //implementation project(":dialer.contactactions")
              implementation project(":dialer.callstats")
              implementation project(":dialer.contactsfragment")
              implementation project(":dialer.dialpadview")
              implementation project(":dialer.enrichedcall.simulator")
              implementation project(":dialer.interactions")
              implementation project(":dialer.main.impl")
              implementation project(":dialer.main.impl.bottomnav")
              implementation project(":dialer.main.impl.toolbar")
              implementation project(":dialer.notification")
              implementation project(":dialer.oem")
              implementation project(":dialer.phonenumberutil")
              implementation project(":dialer.postcall")
              implementation project(":dialer.searchfragment.common")
              implementation project(":dialer.searchfragment.list")
              implementation project(":dialer.searchfragment.nearbyplaces")
              implementation project(":dialer.shortcuts")
              implementation project(":dialer.speeddial")
              implementation project(":dialer.util")
              implementation project(":dialer.theme")
              implementation project(":dialer.widget")
              implementation project(":dialer.lettertile")
              //implementation project(":dialershared.bubble")
              implementation project(":incallui")
              implementation project(":incallui.answer.impl")
              implementation project(":incallui.answer.impl.affordance")
              implementation project(":incallui.answer.impl.answermethod")
              implementation project(":incallui.answer.impl.hint")
              implementation project(":incallui.audioroute")
              implementation project(":incallui.autoresizetext")
              implementation project(":incallui.commontheme")
              implementation project(":incallui.contactgrid")
              implementation project(":incallui.disconnectdialog")
              implementation project(":incallui.incall.impl")
              implementation project(":incallui.hold")
              implementation project(":incallui.sessiondata")
              implementation project(":incallui.speakerbuttonlogic")
              implementation project(":incallui.telecomeventui")
              implementation project(":incallui.video.impl")
              implementation project(":incallui.video.protocol")
              implementation project(":voicemail.impl")
              implementation project(":voicemail.impl.configui")
              implementation project(":frameworks_ex.common")
              implementation project(":libbackup")
              
              implementation 'com.android.support:support-v13:28.0.0'
              implementation 'com.android.support:support-v4:28.0.0'
              implementation 'com.android.support:support-v4:28.0.0'
              implementation 'com.android.support:appcompat-v7:28.0.0'
              implementation 'com.android.support:recyclerview-v7:28.0.0'
              implementation 'com.android.support:cardview-v7:28.0.0'
              implementation 'com.android.support:design:28.0.0'

              implementation "com.google.guava:guava:23.0"
              implementation 'com.google.errorprone:error_prone_annotations:2.0.18'

              implementation 'com.google.dagger:dagger:2.7'
              annotationProcessor 'com.google.dagger:dagger-compiler:2.7'
              
              implementation 'com.github.bumptech.glide:disklrucache:4.9.0'
              implementation 'com.github.bumptech.glide:gifdecoder:4.9.0'
              implementation 'com.github.bumptech.glide:glide:4.9.0'
              implementation 'com.github.bumptech.glide:annotations:4.9.0'
              annotationProcessor "com.github.bumptech.glide:compiler:4.9.0"

              implementation "javax.annotation:javax.annotation-api:1.2"
              implementation "me.leolin:ShortcutBadger:1.1.13"
              implementation "javax.inject:javax.inject:1"
              implementation 'commons-io:commons-io:2.4'
              implementation 'org.apache.james:apache-mime4j-core:0.7.2'
              implementation 'org.apache.james:apache-mime4j-dom:0.7.2'

              implementation 'io.grpc:grpc-core:1.0.3'
              implementation 'io.grpc:grpc-protobuf-lite:1.0.3'
              implementation 'io.grpc:grpc-okhttp:1.0.3'
              implementation 'io.grpc:grpc-stub:1.0.3'
              implementation 'io.grpc:grpc-all:1.0.3'
              implementation 'io.grpc:grpc-context:1.0.3'

              implementation "com.googlecode.libphonenumber:libphonenumber:8.9.5"
              implementation "com.googlecode.libphonenumber:geocoder:2.94"
              implementation "commons-io:commons-io:2.6"

              implementation "com.android.volley:volley:1.1.1"
              
              implementation "com.google.zxing:core:3.4.0"

              implementation "com.google.auto.value:auto-value:1.5.2"
              annotationProcessor "com.google.auto.value:auto-value:1.5.2"

              implementation fileTree(dir: rootProject.projectDir.toString() + "/lib", include: ['lineage-sdk.jar'])
            }
            break
        case ':contacts.common':
            apply plugin: 'com.android.library'
            dependencies {
                implementation project(":dialer.lettertile")
                implementation project(":dialer.theme")
                implementation project(":assets.quantum")
                implementation project(":dialer.contactphoto")
            }
            break
        case ':dialer.dialpadview':
            apply plugin: 'com.android.library'
            dependencies {
                implementation project(":assets.quantum")
            }
            break
        case ':dialer.calllog.ui':
            apply plugin: 'com.android.library'
            dependencies {
                implementation project(":assets.quantum")
            }
            break
        case ':dialer.app':
            apply plugin: 'com.android.library'
            dependencies {
                implementation project(":dialer.widget")
                implementation project(":assets.quantum")
                implementation project(":dialer.theme")
                implementation project(":dialer.dialpadview")
                implementation project(":dialer.blocking")
                implementation project(":dialer.util")
            }
            break
        case ':dialer.contactsfragment':
            apply plugin: 'com.android.library'
            dependencies {
                implementation project(":dialer.widget")
            }
            break
        case ':dialer.lettertile':
            apply plugin: 'com.android.library'
            dependencies {
                implementation project(":assets.product")
                implementation project(":assets.quantum")
            }
            break
        case ':dialer.calllogutils':
            apply plugin: 'com.android.library'
            dependencies {
                implementation project(":assets.product")
                //implementation project(":assets.quantum")
            }
            break
        // case ':dialer.enrichedcall.simulator':
        //     apply plugin: 'com.android.library'
        //     dependencies {
        //         implementation project(':dialer.theme')
        //         implementation project(':assets.quantum')
        //         implementation project(':dialer.blocking')
        //         implementation project(':dialer.dialpadview')
        //         implementation project(':dialer.widget')
        //     }
        //     break
        default:
            apply plugin: 'com.android.library'
            android.sourceSets.main.res.srcDirs = []
            break
        }
    android {
        compileSdkVersion 28

        lintOptions { 
            disable 'MissingTranslation','ExtraTranslation','NotSibling','ProtectedPermissions','AppCompatResource','ImpliedQuantity','RtlCompat','AppLinkUrlError','MissingDefaultResource','StringFormatMatches','UsesMinSdkAttributes'
            abortOnError false
        }

        defaultConfig {
            minSdkVersion 28
            targetSdkVersion 28
            versionCode 1
            versionName "1.0"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        sourceSets {
            main.res.srcDirs = [subproject.projectDir.path + '/res']
            main.manifest.srcFile (subproject.projectDir.path + '/AndroidManifest.xml')
        }
    }
}