import groovy.json.JsonParserType

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10'
    }
}

//tasks.whenTaskAdded { task ->
//    if(task.name.contains('createMockableJar')) {
//        task.enabled = false
//    }
//    println task.name + task.enabled
//}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    buildDir = rootProject.projectDir.toString() + "/build/${project.name}"
    def deps = new groovy.json.JsonSlurper().setType(JsonParserType.LAX).parse(file(rootProject.projectDir.toString() + '/deps.json'))
    project.ext.set("deps", deps)
}

subprojects{ subproject ->
    if(subproject.name == 'Lineage_Dialer'){
        apply plugin:  'com.android.application'
        apply plugin: 'com.google.protobuf'

        protobuf {
            protoc {
                artifact = 'com.google.protobuf:protoc:3.9.2'
            }
            plugins {
                javalite {
                    artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
                }
            }
            generateProtoTasks {
                all().each { task ->
                    task.builtins {
                        remove java
                    }
                    task.plugins {
                        javalite {}
                    }
                }
            }
        }
    }
    else {
        apply plugin: 'com.android.library'
    }

    dependencies {
        def subdeps = deps[subproject.path]
        if(subdeps){
            subdeps.each{
                if(it[0] == ':') {
                    implementation project(it)
                }
                else {
                    implementation it
                }
            }
        }
    }

    android {
        compileSdkVersion 28

        lintOptions { 
            disable 'MissingTranslation','ExtraTranslation','NotSibling','ProtectedPermissions','AppCompatResource','ImpliedQuantity','RtlCompat','AppLinkUrlError','MissingDefaultResource','StringFormatMatches','UsesMinSdkAttributes'
        }

        defaultConfig {
            minSdkVersion 28
            targetSdkVersion 28
            versionCode 1
            versionName "1.0"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        sourceSets {
            def subProjectDir = subproject.projectDir.toString()
            def resDir = [ subProjectDir + '/res/' ]
            def manifestFile = subProjectDir + '/AndroidManifest.xml'

            main {
                res.srcDirs = resDir
                manifest.srcFile manifestFile

                if(subproject.name == 'Lineage_Dialer') {
                    java {
                        srcDirs = [subproject.projectDir.toString()]
                        gradle.blacklist.each{
                            exclude ('**/' + it + '/*.java')
                        }
                    }
                    proto {
                        srcDirs = [subproject.projectDir.toString()]
                    }
                }
            }
        }
    }
}